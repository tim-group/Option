allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'maven'

  group = "com.timgroup"
  releaseVersion = '1.0'
  version = version(project)


  dependencies {
    testCompile     group: "junit",         name: 'junit-dep',           version: "4.9"
    testCompile     group: 'org.hamcrest',  name: 'hamcrest-all',        version: '1.1'
    testCompile    (group: 'org.mockito',   name: 'mockito-core',        version: '1.8.5') {
              exclude module: 'hamcrest-core'
    }
  }

  repositories {
      mavenCentral()
  }

  task wrapper(type: Wrapper) {
      gradleVersion = '0.9.2'
  }

  task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
  }

  artifacts {
       archives sourcesJar
  }
}

project('Option-Utils') {
  dependencies {
      compile     project(':Option-Main')
  }
}

project('Option-Main') {
  dependencies {
    compile     group: 'com.timgroup',                  name: 'Functional',             version: '1.0'
    compile     group: 'com.googlecode.lambdaj',        name: 'lambdaj',                version: '2.3.1'
  }

  repositories {
      mavenRepo name: 'lambdaj', urls: 'http://lambdaj.googlecode.com/svn/repo/releases'
  }
}

project('Option-Scala') {
  scalaVersion = "2.9.0-1"

  dependencies {
    compile     project(':Option-Main')
    compile     "org.scala-lang:scala-library:$scalaVersion"
  }
}

dependsOnChildren()

subprojects.each { subproject ->
    evaluationDependsOn(subproject.path)
}

task jarAll(type: Jar, dependsOn: subprojects.assemble) {
    baseName = 'Option-all'
    subprojects.each { subproject ->
        if (!subproject.name.contains('Scala'))
            from subproject.configurations.archives.allArtifactFiles.collect { zipTree(it) }
    }
}

def version(Project project) {
    def releaseVersion = '0';
    if (project.hasProperty('releaseVersion') && !project.releaseVersion.isEmpty()) {
        releaseVersion = project.releaseVersion
    }

    String projectVersion
    def ciBuildNumber = System.getenv("BUILD_NUMBER")
    if (ciBuildNumber == null || ciBuildNumber.isEmpty()) {
        projectVersion = releaseVersion + '.0.' + (new Date()).format("yyyyMMdd-HHmmss") + '-dev'
    } else {
        projectVersion = releaseVersion + '.' + ciBuildNumber
    }
    return projectVersion
}

if (!hasProperty("sonatypeUsername")) sonatypeUsername = ""
if (!hasProperty("sonatypePassword")) sonatypePassword = ""

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.archives

    repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: "${sonatypeUsername}", password: "${sonatypePassword}")
    }

    pom.project {
      name 'Option'
      packaging 'jar'
      description 'Classes that represent optional values without using null references'
      url 'https://github.com/youdevise/Option'
      inceptionYear '2011'

      scm {
        url 'https://github.com/youdevise/Option'
        connection 'https://github.com/youdevise/Option'
      }

      licenses {
        license {
          name 'MIT License'
          comments """
                /**
Option. Copyright (c) 2011 TIM Group, Ltd.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
                **/
          """
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'Pawel'
          name 'Paweł Krupiński'
          email 'pawel.krupinski"timgroup.com'
          url 'http://www.timgroup.com'
          roles {
            role 'Developer'
          }
          organization 'TIM Group'
          organizationUrl 'http://www.timgroup.com'

          timezone '0'
        }
      }
    }
  }
}